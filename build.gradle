
buildscript {
  repositories { mavenCentral() }
  dependencies { classpath 'net.saliman:gradle-cobertura-plugin:2.0.0' }
  dependencies { classpath 'org.ajoberstar:gradle-git:0.6.3' }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'cobertura'
apply plugin: 'github-pages'

apply from: 'repos.gradle'
apply from: 'sonar.gradle'
apply from: 'docs.gradle'

group = 'com.aestasit.cloud.aws'
version = '0.2-SNAPSHOT'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

dependencies {
  compile localGroovy()  
  compile gradleApi() 
  compile 'com.amazonaws:aws-java-sdk:1.3.33'
  compile 'org.codehaus.groovy:groovy-all:1.8.6'
  compile('com.jcraft:jsch:0.1.44-1') {
    exclude module: 'slf4j-simple'
  }
  compile 'org.apache.commons:commons-lang3:3.1'
  testCompile 'junit:junit:4.11'
  testRuntime 'ch.qos.logback:logback-core:1.0.7'
  testRuntime 'ch.qos.logback:logback-classic:1.0.7'
  testCompile 'org.apache.commons:commons-io:1.3.2'
}  

configurations {
  all*.exclude group: 'commons-logging'
  all*.exclude group: 'log4j'
  all*.exclude module: 'slf4j-simple'
}

test.doFirst {
  systemProperties = System.getProperties()
  project.properties.each { key, value ->
    if (key.startsWith("aws")) {
      systemProperties[key] = value
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.8'
}




